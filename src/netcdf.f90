module netcdf_mod
    use constants, only: dp
    use netcdf

    implicit none
    private

    public :: netcdf_t

    integer, parameter :: max_vars = 100

    type :: var_info_t
        character(len=100) :: name
        integer :: var_id
    end type var_info_t

    type :: netcdf_t
        integer :: ncid = -1
        logical :: is_open = .false.
        logical :: in_define_mode = .false.
        integer :: n_vars = 0
        type(var_info_t) :: vars(max_vars)
    contains
        procedure :: create => netcdf_create
        procedure :: open => netcdf_open
        procedure :: add_global_attribute => netcdf_add_global_attr
        procedure :: read_global_attribute => netcdf_read_global_attr
        procedure :: add_real => netcdf_add_real
        procedure :: add_real_attr => netcdf_add_real_attr
        procedure :: read_real_attr => netcdf_read_real_attr
        procedure :: end_define => netcdf_end_define
        procedure :: write_real => netcdf_write_real
        procedure :: read_real => netcdf_read_real
        procedure :: close => netcdf_close
        final :: netcdf_final
    end type netcdf_t

contains

    subroutine netcdf_create(this, filename)
        class(netcdf_t), intent(inout) :: this
        character(len=*), intent(in) :: filename
        integer :: status

        if (this%is_open) then
            call this%close()
        end if

        status = nf90_create(filename, ior(NF90_CLOBBER, NF90_CLASSIC_MODEL), this%ncid)
        call check_netcdf_status(status, "creating file: "//filename)

        this%is_open = .true.
        this%in_define_mode = .true.
        this%n_vars = 0
    end subroutine netcdf_create

    subroutine netcdf_add_global_attr(this, attr_name, attr_value)
        class(netcdf_t), intent(inout) :: this
        character(len=*), intent(in) :: attr_name
        character(len=*), intent(in) :: attr_value
        integer :: status

        if (.not. this%is_open) then
            error stop "NetCDF file not open"
        end if

        if (.not. this%in_define_mode) then
            error stop "Not in define mode"
        end if

        status = nf90_put_att(this%ncid, NF90_GLOBAL, attr_name, attr_value)
        call check_netcdf_status(status, "setting global attribute: " &
                                 //attr_name)
    end subroutine netcdf_add_global_attr

    subroutine netcdf_add_real(this, var_name)
        class(netcdf_t), intent(inout) :: this
        character(len=*), intent(in) :: var_name
        integer :: status, var_id

        if (.not. this%is_open) then
            error stop "NetCDF file not open"
        end if

        if (.not. this%in_define_mode) then
            error stop "Not in define mode"
        end if

        if (this%n_vars >= max_vars) then
            error stop "Maximum number of variables exceeded"
        end if

        status = nf90_def_var(this%ncid, var_name, NF90_DOUBLE, varid=var_id)
        call check_netcdf_status(status, "defining variable: "//var_name)

        this%n_vars = this%n_vars + 1
        this%vars(this%n_vars)%name = var_name
        this%vars(this%n_vars)%var_id = var_id
    end subroutine netcdf_add_real

    subroutine netcdf_add_real_attr(this, var_name, attr_name, &
                                    attr_value)
        class(netcdf_t), intent(inout) :: this
        character(len=*), intent(in) :: var_name
        character(len=*), intent(in) :: attr_name
        character(len=*), intent(in) :: attr_value
        integer :: status, var_id, i

        if (.not. this%is_open) then
            error stop "NetCDF file not open"
        end if

        if (.not. this%in_define_mode) then
            error stop "Not in define mode"
        end if

        var_id = -1
        do i = 1, this%n_vars
            if (trim(this%vars(i)%name) == trim(var_name)) then
                var_id = this%vars(i)%var_id
                exit
            end if
        end do

        if (var_id == -1) then
            error stop "Variable not found: "//var_name
        end if

        status = nf90_put_att(this%ncid, var_id, attr_name, attr_value)
        call check_netcdf_status(status, "setting attribute "//attr_name &
                                 //" for variable: "//var_name)
    end subroutine netcdf_add_real_attr

    subroutine netcdf_end_define(this)
        class(netcdf_t), intent(inout) :: this
        integer :: status

        if (.not. this%is_open) then
            error stop "NetCDF file not open"
        end if

        if (.not. this%in_define_mode) then
            return
        end if

        status = nf90_enddef(this%ncid)
        call check_netcdf_status(status, "ending definition mode")

        this%in_define_mode = .false.
    end subroutine netcdf_end_define

    subroutine netcdf_write_real(this, var_name, value)
        class(netcdf_t), intent(inout) :: this
        character(len=*), intent(in) :: var_name
        real(dp), intent(in) :: value
        integer :: status, var_id, i

        if (.not. this%is_open) then
            error stop "NetCDF file not open for writing"
        end if

        if (this%in_define_mode) then
            call this%end_define()
        end if

        var_id = -1
        do i = 1, this%n_vars
            if (trim(this%vars(i)%name) == trim(var_name)) then
                var_id = this%vars(i)%var_id
                exit
            end if
        end do

        if (var_id == -1) then
            error stop "Variable not found: "//var_name
        end if

        status = nf90_put_var(this%ncid, var_id, value)
        call check_netcdf_status(status, "writing variable: "//var_name)
    end subroutine netcdf_write_real

    subroutine netcdf_close(this)
        class(netcdf_t), intent(inout) :: this
        integer :: status

        if (this%is_open) then
            if (this%in_define_mode) then
                call this%end_define()
            end if

            status = nf90_close(this%ncid)
            call check_netcdf_status(status, "closing NetCDF file")
            this%is_open = .false.
            this%in_define_mode = .false.
            this%ncid = -1
            this%n_vars = 0
        end if
    end subroutine netcdf_close

    subroutine netcdf_open(this, filename)
        class(netcdf_t), intent(inout) :: this
        character(len=*), intent(in) :: filename
        integer :: status

        if (this%is_open) then
            call this%close()
        end if

        status = nf90_open(filename, NF90_NOWRITE, this%ncid)
        call check_netcdf_status(status, "opening file: "//filename)

        this%is_open = .true.
        this%in_define_mode = .false.
        this%n_vars = 0
    end subroutine netcdf_open

    subroutine netcdf_read_real(this, var_name, value)
        class(netcdf_t), intent(inout) :: this
        character(len=*), intent(in) :: var_name
        real(dp), intent(out) :: value
        integer :: status, var_id

        if (.not. this%is_open) then
            error stop "NetCDF file not open for reading"
        end if

        status = nf90_inq_varid(this%ncid, var_name, var_id)
        call check_netcdf_status(status, "finding variable: "//var_name)

        status = nf90_get_var(this%ncid, var_id, value)
        call check_netcdf_status(status, "reading variable: "//var_name)
    end subroutine netcdf_read_real

    subroutine netcdf_read_global_attr(this, attr_name, attr_value)
        class(netcdf_t), intent(inout) :: this
        character(len=*), intent(in) :: attr_name
        character(len=*), intent(out) :: attr_value
        integer :: status

        if (.not. this%is_open) then
            error stop "NetCDF file not open for reading"
        end if

        status = nf90_get_att(this%ncid, NF90_GLOBAL, attr_name, attr_value)
        call check_netcdf_status(status, "reading global attribute: " &
                                 //attr_name)
    end subroutine netcdf_read_global_attr

    subroutine netcdf_read_real_attr(this, var_name, attr_name, attr_value)
        class(netcdf_t), intent(inout) :: this
        character(len=*), intent(in) :: var_name
        character(len=*), intent(in) :: attr_name
        character(len=*), intent(out) :: attr_value
        integer :: status, var_id

        if (.not. this%is_open) then
            error stop "NetCDF file not open for reading"
        end if

        status = nf90_inq_varid(this%ncid, var_name, var_id)
        call check_netcdf_status(status, "finding variable: "//var_name)

        status = nf90_get_att(this%ncid, var_id, attr_name, attr_value)
        call check_netcdf_status(status, "reading attribute "//attr_name &
                                 //" for variable: "//var_name)
    end subroutine netcdf_read_real_attr

    subroutine netcdf_final(this)
        type(netcdf_t), intent(inout) :: this

        if (this%is_open) then
            call this%close()
        end if
    end subroutine netcdf_final

    subroutine check_netcdf_status(status, operation)
        integer, intent(in) :: status
        character(len=*), intent(in) :: operation

        if (status /= NF90_NOERR) then
            print *, "NetCDF error during ", operation, ":"
            print *, trim(nf90_strerror(status))
            error stop "NetCDF operation failed"
        end if
    end subroutine check_netcdf_status

end module netcdf_mod
